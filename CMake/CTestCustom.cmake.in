#/*================================================================================
#
#  NiftyLink:  A software library to facilitate communication over OpenIGTLink. 
#  
#  Copyright (c) University College London (UCL). All rights reserved.
#
#  This software is distributed WITHOUT ANY WARRANTY; without even
#  the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR
#  PURPOSE.  
#
#  See LICENSE.txt in the top level directory for details. 
#
#=================================================================================*/
set (CTEST_EXTRA_COVERAGE_GLOB ${CTEST_EXTRA_COVERAGE_GLOB})
foreach (E IN ITEMS .c .cxx .h .txx)
  list (APPEND CTEST_EXTRA_COVERAGE_GLOB "Code/Libraries/Common/NiftyLink*${E}")
  list (APPEND CTEST_EXTRA_COVERAGE_GLOB "Code/Libraries/Descriptors/NiftyLink*${E}")
  list (APPEND CTEST_EXTRA_COVERAGE_GLOB "Code/Libraries/MessageHandling/NiftyLink*${E}")
  list (APPEND CTEST_EXTRA_COVERAGE_GLOB "Code/Libraries/NetworkQt/NiftyLink*${E}")

  # This stuff is considered obsolete.
  # list (APPEND CTEST_EXTRA_COVERAGE_GLOB "Code/Libraries/NetworkOpenIGTLink/NiftyLink*${E}")
  # list (APPEND CTEST_EXTRA_COVERAGE_GLOB "Code/Libraries/NetworkOriginal/NiftyLink*${E}")
  # list (APPEND CTEST_EXTRA_COVERAGE_GLOB "Code/Libraries/MessageWrappers/NiftyLink*${E}")

  # We don't want coverage for this, as it isn't ours.
  # list (APPEND CTEST_EXTRA_COVERAGE_GLOB "Code/Libraries/Common/Qs*${E}")

  # This is a GUI client, which is just a thin wrapper around other classes, so will ignore for now.
  # list (APPEND CTEST_EXTRA_COVERAGE_GLOB "Code/Applications/*${E}")

endforeach ()

set(CTEST_CUSTOM_COVERAGE_EXCLUDE
  ${CTEST_CUSTOM_COVERAGE_EXCLUDE}

  # Exclude try_compile sources from coverage results:
  ".*/CMakeFiles/CMakeTmp/.*"

  # Exclude files generated by the moc pre-compiler
  ".*/moc_.*"

  # Exclude files generated by the uic pre-compiler
  ".*/ui_.*"

  # Exclude files from any external project
  ".*/CMakeExternals/.*"

  )

# The following tests should not be run under valgrind
set(CTEST_CUSTOM_MEMCHECK_IGNORE
  KWStyleCodeTest
  )

set(CTEST_CUSTOM_ERROR_MATCH
  ${CTEST_CUSTOM_ERROR_MATCH}
  "CMake Error[ :]"
  )

set(CTEST_CUSTOM_WARNING_MATCH
  ${CTEST_CUSTOM_WARNING_MATCH}
  "CMake Warning[ :]"
  )

set(CTEST_CUSTOM_WARNING_EXCEPTION
  ${CTEST_CUSTOM_WARNING_EXCEPTION}
  
  # kwstyle suppressions
  "[Kk][Ww][Ss]tyle.*kws.*cxx"
  "[Kk][Ww][Ss]tyle.*kws.*h"
  "[Kk][Ww][Ss]tyle.*metaCommand.*cxx"

  # Qt suppressions.
  "include.[Qq]t([Cc]ore|[Gg]ui).*warning C4127: conditional expression is constant"
  "[Qq]t.*h.*warning.*declaration of .* shadows a member of .this"
  "[Qq]t.*h.*warning.*(copy constructor|assignment operator) could not be generated"  

  # Suppress warning caused when QT 'foreach' loops are combined
  ".*warning: declaration of '_container_' shadows a previous local"
  
  # For NifTK in Trac #1278 and subsequently Trac #1287 for NiftyLink. Suppress Qt warnings on Lion
  ".*[Qq][Gg]lobal.h.*warning.*This version of Mac OS X is unsupported.*"
  
  # Suppressing warnings about duplicate libraries in Darwin
  # At some point this may be addressed by CMake feature request: 
  # http://public.kitware.com/Bug/view.php?id=10179
  "ld: warning: duplicate dylib.*"
  
  # Visual studio spurious warnings...
  "The following environment variables were not found"
  
  # Ignore MOC warning / error when header doesn't contain Qt stuff
  ".*No relevant classes found. No output generated.*"
  
  # Ignore stuff comming from external projects, so dashboards are focussed on this project.
  ".*/CMakeExternals/.*"

  )
